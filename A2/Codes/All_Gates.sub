.subckt NAND2IN node_out node_a node_b vdd gnd

    Mn1 node_out node_a node_y gnd CMOSN W = {wn1} L = {ln1}
    + AS = {5*wn1*LAMBDA} PS = {10*LAMBDA + 2*wn1} AD = {5*wn1*LAMBDA} PD = {10*LAMBDA + 2*wn1}

    Mn2 node_y node_b gnd gnd CMOSN W = {wn2} L = {ln2}
    + AS = {5*wn2*LAMBDA} PS = {10*LAMBDA + 2*wn2} AD = {5*wn2*LAMBDA} PD = {10*LAMBDA + 2*wn2}


    Mp1 node_out node_a vdd vdd CMOSP W = {wp1} L = {lp1}
    + AS = {5*wp1*LAMBDA} PS = {10*LAMBDA + 2*wp1} AD = {5*wp1*LAMBDA} PD = {10*LAMBDA + 2*wp1}

    Mp2 node_out node_b vdd vdd CMOSP W = {wp2} L = {lp2}
    + AS = {5*wp2*LAMBDA} PS = {10*LAMBDA + 2*wp2} AD = {5*wp2*LAMBDA} PD = {10*LAMBDA + 2*wp2}

.ends NAND2IN

.subckt NOR2IN node_out node_a node_b vdd gnd

    Mp1 node_out node_a node_y vdd CMOSP W = {wn1} L = {ln1}
    + AS = {5*wn1*LAMBDA} PS = {10*LAMBDA + 2*wn1} AD = {5*wn1*LAMBDA} PD = {10*LAMBDA + 2*wn1}

    Mp2 node_y node_b vdd vdd CMOSP W = {wn2} L = {ln2}
    + AS = {5*wn2*LAMBDA} PS = {10*LAMBDA + 2*wn2} AD = {5*wn2*LAMBDA} PD = {10*LAMBDA + 2*wn2}


    Mn1 node_out node_a gnd gnd CMOSN W = {wp1} L = {lp1}
    + AS = {5*wp1*LAMBDA} PS = {10*LAMBDA + 2*wp1} AD = {5*wp1*LAMBDA} PD = {10*LAMBDA + 2*wp1}

    Mn2 node_out node_b gnd gnd CMOSN W = {wp2} L = {lp2}
    + AS = {5*wp2*LAMBDA} PS = {10*LAMBDA + 2*wp2} AD = {5*wp2*LAMBDA} PD = {10*LAMBDA + 2*wp2}

.ends NOR2IN

.subckt NOT node_out node_a vdd gnd
    Mn1 node_out node_a gnd gnd CMOSN W = {wn1} L = {ln1}
    + AS = {5*wn1*LAMBDA} PS = {10*LAMBDA + 2*wn1} AD = {5*wn1*LAMBDA} PD = {10*LAMBDA + 2*wn1}

    Mp1 node_out node_a vdd vdd CMOSP W = {wn2} L = {ln2}
    + AS = {5*wn2*LAMBDA} PS = {10*LAMBDA + 2*wn2} AD = {5*wn2*LAMBDA} PD = {10*LAMBDA + 2*wn2}
.ends NOT

.subckt AND2IN node_out node_a node_b vdd gnd
    X1 node_c node_a node_b vdd gnd NAND2IN
    X2 node_out node_c vdd gnd NOT
.ends AND2IN

.subckt AND3IN node_out node_a node_b node_c vdd gnd
    X1 node_x node_a node_b vdd gnd AND2IN
    X2 node_out node_x node_c vdd gnd AND2IN
.ends AND3IN

.subckt AND4IN node_out node_a node_b node_c node_d vdd gnd
    X1 node_x node_a node_b vdd gnd AND2IN
    X2 node_y node_c node_d vdd gnd AND2IN
    X3 node_out node_x node_y vdd gnd AND2IN
.ends AND4IN

.subckt AND5IN node_out node_a node_b node_c node_d node_e vdd gnd
    X1 node_x node_a node_b vdd gnd AND2IN
    X2 node_y node_c node_d node_e vdd gnd AND3IN
    X3 node_out node_x node_y vdd gnd AND2IN
.ends AND5IN

.subckt OR2IN node_out node_a node_b vdd gnd
    X1 node_c node_a node_b vdd gnd NOR2IN
    X2 node_out node_c vdd gnd NOT
.ends OR2IN

.subckt OR3IN node_out node_a node_b node_c vdd gnd
    X1 node_x node_a node_b vdd gnd OR2IN
    X2 node_out node_x node_c vdd gnd OR2IN
.ends OR3IN

.subckt OR4IN node_out node_a node_b node_c node_d vdd gnd
    X1 node_x node_a node_b vdd gnd OR2IN
    X2 node_y node_c node_d vdd gnd OR2IN
    X3 node_out node_x node_y vdd gnd OR2IN
.ends OR4IN

.subckt XOR2IN node_out node_a node_b vdd gnd
    X1 node_c node_a node_b vdd gnd NAND2IN
    X2 node_d node_a node_c vdd gnd NAND2IN
    X3 node_e node_b node_c vdd gnd NAND2IN
    X4 node_out node_d node_e vdd gnd NAND2IN
.ends XOR2IN

.subckt XNOR2IN node_out node_a node_b vdd gnd
    X1 node_x node_a node_b vdd gnd XOR2IN
    X2 node_out node_x vdd gnd NOT
.ends XNOR2IN